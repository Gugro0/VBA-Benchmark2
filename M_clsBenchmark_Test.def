Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
'---------------------------------------------------------------------------------------
' Module    : clsBenchmark_Test
' Author    : K.Gundermann
' Date      : 21.07.2012
' Purpose   : Unit Test for clsBenchmark
'---------------------------------------------------------------------------------------

Option Compare Text
Option Explicit

'AccUnit:TestClass
Dim objBenchmark As clsBenchmark

' AccUnit infrastructure for advanced AccUnit features. Do not remove these lines.
Implements SimplyVBUnit.ITestFixture
Implements AccUnit_Integration.ITestManagerBridge
Private TestManager As AccUnit_Integration.TestManager
Private Sub ITestManagerBridge_InitTestManager(ByVal NewTestManager As AccUnit_Integration.ITestManagerComInterface): Set TestManager = NewTestManager: End Sub
Private Function ITestManagerBridge_GetTestManager() As AccUnit_Integration.ITestManagerComInterface: Set ITestManagerBridge_GetTestManager = TestManager: End Function
Private Sub ITestFixture_AddTestCases(ByVal Tests As SimplyVBUnit.TestCaseCollector): TestManager.AddTestCases Tests: End Sub

'--------------------------------------------------------------------
' Tests
'--------------------------------------------------------------------
Public Sub Setup()
  Set objBenchmark = New clsBenchmark
  objBenchmark.MinIterations = 1
  objBenchmark.Timeout = 0
End Sub

Public Sub CreateBenchmark_withClass_hasClassNameasDescription()
  ' Act
  objBenchmark.Run New clsSimple_Bench
  
  ' Assert
  Assert.That objBenchmark.Title, Text.Matches("Running Benchmark clsSimple_Bench")
End Sub

' AccUnit:Tags(Example, Simple Test)
Public Sub CreateBenchmark_withClass_has4Tests()
  
  ' Act
  objBenchmark.Run New clsSimple_Bench
  
  ' Assert
  Assert.That objBenchmark.TestCount, Iz.EqualTo(4)
End Sub

' AccUnit:Tags(Example, Simple Test)
Public Sub CreateBenchmark_withClass_hasTestname()
  ' Arrange
  
  ' Act
  objBenchmark.Run New clsSimple_Bench
  
  ' Assert
  Assert.AreEqualStrings "IntegerBenchmark", objBenchmark.Results.Test(1).Name
  Assert.AreEqualStrings "LongBenchmark", objBenchmark.Results.Test(2).Name
  Assert.AreEqualStrings "DoubleBenchmark", objBenchmark.Results.Test(3).Name
  Assert.AreEqualStrings "CurrencyBenchmark", objBenchmark.Results.Test(4).Name
  
End Sub

Public Sub a_Testrun_needs_some_time()

  ' Act
  objBenchmark.Run New clsSimple_Bench
  
  ' Assert
  Assert.That objBenchmark.Results.Test(1).Duration, Iz.GreaterThan(1 / 100)  ' Needs more tahn 1/100 msec

End Sub

'Public Sub a_Testrun_with_more_iteration_needs_more_time()
'
'  ' Act
'  objBenchmark.SetIterations(objBenchmark.Iterations * 10).Run New clsSimple_Bench
'
'  ' Assert
'  Assert.That objBenchmark.Results.Test(1).Duration, Iz.GreaterThan(10)
'
'End Sub

Public Sub we_have_a_TestResult()
  
  ' Act
  objBenchmark.Run New clsSimple_Bench
  
  ' Assert
  With objBenchmark.Results.Test(1)
    Assert.That .Result, Text.Matches("IntegerBenchmark .*[0-9]*\,[0-9]* msec/Iter .* ")
  End With
  
End Sub

Public Sub We_have_a_fastest_Test()
  ' Act
  objBenchmark.Run New clsSimple_Bench
  ' Assert
  Assert.IsNotNothing objBenchmark.Results.Fastest
End Sub

Public Sub We_have_a_slowest_Test()
  ' Act
  objBenchmark.Run New clsSimple_Bench
  ' Assert
  Assert.IsNotNothing objBenchmark.Results.Slowest
End Sub

Public Sub We_Have_a_TestReport()

  ' Act
  objBenchmark.Run New clsSimple_Bench
  ' Assert
  Assert.That objBenchmark.Report, Iz.[Not].EqualTo("")
  Assert.That objBenchmark.Report, Text.Contains("Fastest")
End Sub

' ------------------------------------------------------------------------
' -- Running a very fast method

Public Sub We_can_run_EmptyMethods_Test()
  ' Act
  objBenchmark.Run New clsNull_Bench
  ' Assert
  Assert.That objBenchmark.Report, Iz.[Not].EqualTo("")
  
  Assert.That objBenchmark.Results.Test(1).Duration, Iz.EqualTo(0)
  Assert.IsFalse objBenchmark.Results.Test(1).IsValidTest

End Sub

' ------------------------------------------------------------------------
' -- Running a slow method

Public Sub We_can_run_LongRunning_Test()
  ' Act
  objBenchmark.Run New clsLongRunning_Bench
  ' Assert
  Assert.That objBenchmark.Report, Iz.[Not].EqualTo("")
  Assert.That objBenchmark.Report, Text.Contains("Fastest")

End Sub


' ------------------------------------------------------------------------

Public Sub We_can_run_Test_by_Name()
  ' Act
  objBenchmark.MinIterations = 100
  'objBenchmark.Run "clsSimple_Bench"
  objBenchmark.Run New clsSimple_Bench
  
  ' Assert
  Assert.That objBenchmark.TestCount, Iz.EqualTo(4)
  Assert.That objBenchmark.Report, Text.Contains("The fastest Test")
  Assert.That objBenchmark.Results.Test(1).Iterations, Iz.EqualTo(100)

End Sub