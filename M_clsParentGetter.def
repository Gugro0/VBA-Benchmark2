Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
Option Compare Database
Option Explicit

'---------------------------------------------------------------------------------------
' Module    : clsParentGetter
' Author    : K.Gundermann
' Date      : 15.06.2012
' Purpose   : Provides Reference to Parent Object
'             Given: a ParentObject creates a ChildObject and holds a reference to it
'             When:  the ChildObject needs to access Properties of the ParentObject
'             ItCouldNot: store a reference to the ParentObject as this gives
'                         a circular reference, both objects will never be removed
'                         by the garbage collector
'             ItShould: use the ParentGetter to query for the ParentObject
'
'---------------------------------------------------------------------------------------
' In ParentObject:
'
' Private WithEvents c_ParentGetter As clsParentGetter
'
' Private Sub Class_Initialize()
'  Set c_ParentGetter = New clsParentGetter
' End Sub
'
' Private Sub c_ParentGetter_QueryParent(ByRef parent As Object)
'  Set parent = Me
' End Sub
'
' Private Sub CreateChild()
'   dim objChild as clsChild
'
'   Set objChild = New clsChild
'   Set objChild.ParentGetter = c_ParentGetter
'
'   ' Or with Factory:
'   set objChild = Factory.NewChild(Param, .., c_ParentGetter
' End Sub
'
'---------------------------------------------------------------------------------------
' In ChildObject:
'
' Private c_ParentGetter As clsParentGetter
'
' Public Property Set ParentGetter(Byval pg as clsParentGetter)
'   set c_ParentGetter = pg
' End Property
'
' Private Property Get Parent() as clsParent
'   set Parent = c_ParentGetter.GetParent
' End Property


'---------------------------------------------------------------------------------------

Event QueryParent(ByRef Parent As Object)

Public Function GetParent() As Object
  Dim Parent As Object
  
  RaiseEvent QueryParent(Parent)
  Debug.Assert Not (Parent Is Nothing)
  
  Set GetParent = Parent
  Set Parent = Nothing
End Function